#+title: laooree's Emacs Configuration
#+author: laooree
#+description: My emacs configuration, in org-mode
#+startup: content
#+auto_tangle: t

This is my Emacs configuration, written in org-mode.

* Description

My Emacs configuration is composed by:
- =config.org=
- =custom.el=
The latter contains all the customizations that are made through the =M-x
customize= interface, and the Emacs configuration is set to load it.

The =config.org= file uses the =org-babel-tangle= feature to generate the
following two files:
- =early-init.el=
- =init.el=
This feature concatenates the content of all the emacs-lisp source blocks that
contain a =:tangle "filename.el"= specification in =filename.el=.

** Dependencies

This config is tested with =Emacs >= 29.4=, on Linux.

The config requires:
- =git=;
- =ripgrep=;
- =Iosevka Nerd Font= font;
- =Nerd Icons=, which can be installed running =M-x nerd-icons-install-fonts=.

** Instructions

For the first installation, it's necessary to visit the =config.org= file and
execute =org-babel-tangle= command (=C-c C-v t=). The next times, the tangling
happens automatically every time the the =config-org= file is saved, thanks to
the [[https://github.com/yilkalargaw/org-auto-tangle][org-auto-tangle]] package.

* Early initialization
** Speed up the startup

Based on Nicolas Rougier's [[https://github.com/rougier/dotemacs/blob/master/dotemacs.org][early-init file]]. It disables garbage collection at
startup and enables it afterwards. Most importantly, it disables =package.el=,
as =Elpaca= is used instead.

#+begin_src emacs-lisp :tangle "early-init.el"

  (setq
   gc-cons-threshold most-positive-fixnum    ; Very large threshold for garbage
                                             ; collector during init
   package-enable-at-startup nil)            ; We'll use straight.el

  (setq native-comp-eln-load-path
        (list (expand-file-name "eln-cache" user-emacs-directory)))

  ;; Reset garbage collector limit after init process has ended (8Mo)
  (add-hook 'after-init-hook
            #'(lambda () (setq gc-cons-threshold (* 8 1024 1024))))

#+end_src

** Package manager

#+begin_src emacs-lisp :tangle "early-init.el"
  (defvar elpaca-installer-version 0.11)
  (defvar elpaca-directory (expand-file-name "elpaca/" user-emacs-directory))
  (defvar elpaca-builds-directory (expand-file-name "builds/" elpaca-directory))
  (defvar elpaca-repos-directory (expand-file-name "repos/" elpaca-directory))
  (defvar elpaca-order '(elpaca :repo "https://github.com/progfolio/elpaca.git"
                                :ref nil :depth 1 :inherit ignore
                                :files (:defaults "elpaca-test.el" (:exclude "extensions"))
                                :build (:not elpaca--activate-package)))
  (let* ((repo  (expand-file-name "elpaca/" elpaca-repos-directory))
         (build (expand-file-name "elpaca/" elpaca-builds-directory))
         (order (cdr elpaca-order))
         (default-directory repo))
    (add-to-list 'load-path (if (file-exists-p build) build repo))
    (unless (file-exists-p repo)
      (make-directory repo t)
      (when (<= emacs-major-version 28) (require 'subr-x))
      (condition-case-unless-debug err
          (if-let* ((buffer (pop-to-buffer-same-window "*elpaca-bootstrap*"))
                    ((zerop (apply #'call-process `("git" nil ,buffer t "clone"
                                                    ,@(when-let* ((depth (plist-get order :depth)))
                                                        (list (format "--depth=%d" depth) "--no-single-branch"))
                                                    ,(plist-get order :repo) ,repo))))
                    ((zerop (call-process "git" nil buffer t "checkout"
                                          (or (plist-get order :ref) "--"))))
                    (emacs (concat invocation-directory invocation-name))
                    ((zerop (call-process emacs nil buffer nil "-Q" "-L" "." "--batch"
                                          "--eval" "(byte-recompile-directory \".\" 0 'force)")))
                    ((require 'elpaca))
                    ((elpaca-generate-autoloads "elpaca" repo)))
              (progn (message "%s" (buffer-string)) (kill-buffer buffer))
            (error "%s" (with-current-buffer buffer (buffer-string))))
        ((error) (warn "%s" err) (delete-directory repo 'recursive))))
    (unless (require 'elpaca-autoloads nil t)
      (require 'elpaca)
      (elpaca-generate-autoloads "elpaca" repo)
      (let ((load-source-file-function nil)) (load "./elpaca-autoloads"))))
  (add-hook 'after-init-hook #'elpaca-process-queues)
  (elpaca `(,@elpaca-order))

  ;; Install use-package support
  (elpaca elpaca-use-package
    ;; Enable use-package :ensure support for Elpaca.
    (elpaca-use-package-mode))
#+end_src

** Theme and fonts

This section loads the theme provided by Nixos stylix if system is Nixos,
otherwise installs base16 theme.
#+begin_src emacs-lisp :tangle "early-init.el"

  ;; Default frame title
  (setq-default frame-title-format '("Gnu Emacs"))

  (defun get-linux-distribution ()
    "Get the Linux distribution name from /etc/os-release."
    (when (and (eq system-type 'gnu/linux)
               (file-readable-p "/etc/os-release"))
      (with-temp-buffer
        (insert-file-contents "/etc/os-release")
        (when (re-search-forward "^ID=\\(.+\\)$" nil t)
          (let ((id (match-string 1)))
            ;; Remove possible surrounding quotes
            (replace-regexp-in-string "\"" "" id))))))

  (if (equal "nixos" (get-linux-distribution))
    ;; require stylix theme if true
    (progn
      (require 'base16-stylix-theme)
      (setq base16-theme-256-color-source 'colors)
      (load-theme 'base16-stylix t))
    ;; use base16 theme if false
    (use-package base16-emacs
      :ensure t
      :demand t
      :config
      (load-theme 'base16-nord t)))

  (let ((mono-spaced-font "Iosevka Nerd Font"))
    (set-face-attribute 'default nil :family mono-spaced-font :height 180)
    (set-face-attribute 'fixed-pitch nil :family mono-spaced-font :height 180))

  (elpaca-wait)
#+end_src

* Eldoc

Ensure latest version of =eldoc= is installed. =use-package= is giving some
issues here, so this workaround is used instead. The first three lines ensure
that =eldoc= does not load before =elpaca=, allowing for =elpaca= to update
=eldoc=, which is then loaded.

#+begin_src emacs-lisp :tangle "init.el"
(unload-feature 'eldoc t)
(setq custom-delayed-init-variables '())
(defvar global-eldoc-mode nil)

(elpaca eldoc
  (require 'eldoc)
  (global-eldoc-mode))
#+end_src

* Evil

#+begin_src emacs-lisp :tangle "init.el"

  (use-package evil
    :ensure t
    :demand t

    :init
    (setq evil-want-fine-undo t)
    ;; Required by evil collection
    (setq evil-want-integration t)
    (setq evil-want-keybinding nil)
    (setq evil-undo-system 'undo-redo)

    :config
    (evil-mode 1)
    (add-hook 'org-mode-hook
              (lambda ()
                (evil-local-set-key 'normal (kbd "TAB") 'org-cycle)
                (evil-local-set-key 'normal (kbd "<tab>") 'org-cycle))))

  ;; Evil collection
  (use-package evil-collection
    :after evil
    :ensure t
    :config
    (evil-collection-init))

  ;; Evil numbers
  (use-package evil-numbers
    :ensure t
    :after evil
    :config
    (define-key evil-normal-state-map (kbd "+") 'evil-numbers/inc-at-pt)
    (define-key evil-normal-state-map (kbd "-") 'evil-numbers/dec-at-pt))

  ;; Evil snipe
  (use-package evil-snipe
    :ensure t
    :after evil
    :config
    (evil-snipe-mode +1)
    (evil-snipe-override-mode +1))

#+end_src

* UI and UX
** Some built-in options:

#+begin_src emacs-lisp :tangle "init.el"

  ;; Set different file for M-x customize interface
  (setq custom-file (locate-user-emacs-file "custom.el"))
  (load custom-file :no-error-if-file-is-missing)

  ;; Easily visit recent files
  (recentf-mode 1)

  ;; Wrap lines and set fill-column width
  (setq-default fill-column 80)
  (global-visual-line-mode 1)

  ;; Warn user if trying to open a file bigger than 100MB
  (setq large-file-warning-threshold 100000000)

  ;; Accept y or n as yes or no
  (fset 'yes-or-no-p 'y-or-n-p)

  ;;Require files to end with a newline.
  (setq require-final-newline t)

  ;; Backups and autosaves location
  (setq backup-directory-alist
        `((".*" . ,temporary-file-directory)))
  (setq auto-save-file-name-transforms
        `((".*" ,temporary-file-directory t)))

  ;; Automatically revert buffers
  (global-auto-revert-mode t)

  ;; Scroll one line at time
  (setq-default scroll-conservatively 10000
                scroll-step 1
                scroll-margin 5)

  ;; Prefer utf-8 encoding
  (prefer-coding-system 'utf-8)

  (setq-default visible-bell nil             ;; No visual bell
                ring-bell-function 'ignore)  ;; No bell

  (tool-bar-mode -1)
  (scroll-bar-mode -1)
  (menu-bar-mode -1)

  (setq inhibit-startup-message t)
  ;; (setq initial-scratch-message nil)

  ;; Prevent compilation warnings to pop-up
  (add-to-list 'display-buffer-alist
               '("\\`\\*\\(Warnings\\|Compile-Log\\)\\*\\'"
                 (display-buffer-no-window)
                 (allow-no-window . t)))

  ;; whitespace-mode
  (setq whitespace-style '(face trailing))
  (global-whitespace-mode 1)

  (setq display-line-numbers t)
  (global-display-line-numbers-mode)

 #+end_src

** org-auto-tangle

This package allows to automatically tangle =org= files when they are
saved. When the minor mode is active, it is sufficient to add =#+auto_tangle: t=
in the org file for which you want auto tangling.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package org-auto-tangle
    :ensure t
    :defer t
    :hook (org-mode . org-auto-tangle-mode))
#+end_src

** Dired

Tweak =dired=. Options should be self-explanatory.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package dired
    :ensure nil
    :commands (dired)
    :hook
    ((dired-mode . dired-hide-details-mode)
     (dired-mode . hl-line-mode))
    :config
    (setq dired-recursive-copies 'always)
    (setq dired-recursive-deletes 'always)
    (setq delete-by-moving-to-trash t)
    (setq dired-kill-when-opening-new-dired-buffer t)
    (setq dired-dwim-target t))
#+end_src

The =dired-subtree= allows to expand folders using =TAB= key.

#+begin_src emacs-lisp "init.el"
  (use-package dired-subtree
    :ensure t
    :after (dired)
    :bind
    ( :map dired-mode-map
      ("<tab>" . dired-subtree-toggle)
      ("TAB" . dired-subtree-toggle)
      ("<backtab>" . dired-subtree-remove)
      ("S-TAB" . dired-subtree-remove))
    :config
    (setq dired-subtree-use-backgrounds nil))
#+end_src

** Emoji

#+begin_src emacs-lisp :tangle "init.el"
  (use-package emojify
    :ensure t
    ;;:defer t
    :init
    (setq emojify-display-style 'unicode)
    (setq emojify-emoji-styles '(unicode))
    (when (member "Noto Color Emoji" (font-family-list))
      (set-fontset-font
       t 'symbol (font-spec :family "Noto Color Emoji") nil 'prepend))
    :config
    (bind-key* (kbd "C-c .") #'emojify-insert-emoji)
    :hook
    (after-init . global-emojify-mode))
#+end_src

** Terminal
*** Wayland clipboard
#+begin_src emacs-lisp :tangle "init.el"
  ;; credit: yorickvP on Github
  (when (string= (getenv "XDG_SESSION_TYPE") "wayland")
    (setq wl-copy-process nil)

    (defun wl-copy (text)
      (setq wl-copy-process (make-process :name "wl-copy"
                                          :buffer nil
                                          :command '("wl-copy" "-f" "-n")
                                          :connection-type 'pipe))
      (process-send-string wl-copy-process text)
      (process-send-eof wl-copy-process))

    (defun wl-paste ()
      (if (and wl-copy-process (process-live-p wl-copy-process))
          nil ; should return nil if we're the current paste owner
        (shell-command-to-string "wl-paste -n | tr -d \r")))

    (setq interprogram-cut-function 'wl-copy)
    (setq interprogram-paste-function 'wl-paste))
#+end_src

*** Cursor shape
#+begin_src emacs-lisp :tangle "init.el"
  (defun my/set-cursor-shape ()
    "Set terminal cursor shape based on evil state."
    (let ((shape
           (cond
            ((evil-insert-state-p) "\e[5 q")  ;; Blinking bar
            ((evil-motion-state-p) "\e[0 q")  ;; Block
            ((evil-operator-state-p) "\e[3 q");; Underline
            ((evil-replace-state-p) "\e[3 q") ;; Underline
            ((evil-visual-state-p) "\e[0 q")  ;; Block
            (t "\e[0 q"))))                   ;; Block
      (send-string-to-terminal shape)))

  ;; Hook into Evil state changes
  (add-hook 'post-command-hook #'my/set-cursor-shape)
#+end_src
#+end_src
** Nerd icons

Install and use nerd-icons.

#+begin_src emacs-lisp :tangle "init.el" 
  (use-package nerd-icons
    :defer t
    :ensure t)

  (use-package nerd-icons-completion
    :ensure t
    :after (marginalia)
    :config
    (add-hook 'marginalia-mode-hook #'nerd-icons-completion-marginalia-setup))

  (use-package nerd-icons-corfu
    :ensure t
    :after (corfu)
    :config
    (add-to-list 'corfu-margin-formatters #'nerd-icons-corfu-formatter))

  (use-package nerd-icons-dired
    :ensure t
    :defer t
    :hook
    (dired-mode . nerd-icons-dired-mode))
#+end_src

** Mode-line

#+begin_src  emacs-lisp :tangle "init.el"
  (use-package doom-modeline
    :ensure t
    :init (doom-modeline-mode 1))
#+end_src

** Rainbow csv

Paint each column in =.csv= files with different colors.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package rainbow-csv
    :ensure (rainbow-csv :host github :repo "emacs-vs/rainbow-csv")
    :defer t
    :config
    (add-hook 'csv-mode-hook #'rainbow-csv-mode)
    (add-hook 'tsv-mode-hook #'rainbow-csv-mode))
#+end_src

** Rainbow delimiters

Paint delimiters with colors based on the nesting level. Super useful with lisp,
very useful everywhere else.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package rainbow-delimiters
    :ensure t
    :defer t
    :hook (prog-mode . rainbow-delimiters-mode))
#+end_src

** Rainbow mode

Rainbow mode colorizes color names in buffers. Disabled by default.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package rainbow-mode
    :ensure t
    :defer t)
#+end_src

** Open files with

Specify external programs to open specific file extensions.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package openwith
    :ensure t
    :init
    (setq openwith-associations '(("\\.pdf\\'" "evince" (file))))

    :config
    (openwith-mode t))
#+end_src
** Magit

#+begin_src emacs-lisp :tangle "init.el"
  ;; Make sure transient is up to date
  (use-package transient
    :ensure t
    :defer t)

  ;; Install magit
  (use-package magit
    :ensure t
    :defer t)
#+end_src

** Elpaca wait

#+begin_src emacs-lisp :tangle "init.el"
  (elpaca-wait)
#+end_src

* Completions, suggestions, AI tools
** Vertico

#+begin_src emacs-lisp :tangle "init.el"
  (use-package vertico
    :ensure (vertico :files (:defaults "extensions/*")
                     :includes (vertico-mouse
                                vertico-directory))
    :config (vertico-mode))

  ;;Enable mouse integration
  (use-package vertico-mouse
    :after (vertico)
    :ensure nil
    :config (vertico-mouse-mode))

  ;; Faster navigation in vertico minibuffer
  (use-package vertico-directory
    :after (vertico)
    :ensure nil
    :bind (:map vertico-map
                ("RET" . vertico-directory-enter)
                ("DEL" . vertico-directory-delete-char)
                ("M-DEL" . vertico-directory-delete-word)))

#+end_src

** Marginalia

#+begin_src emacs-lisp :tangle "init.el"
  (use-package marginalia
    :ensure t
    :defer t
    :init (marginalia-mode))
#+end_src

** Orderless

#+begin_src emacs-lisp :tangle "init.el"
  (use-package orderless
    :ensure t
    :defer t
    :init
    (setq completion-styles '(orderless basic)
          completion-category-defaults nil
          completion-category-overrides nil
          read-file-name-completion-ignore-case t
          read-buffer-completion-ignore-case t
          completion-ignore-case t
          orderless-matching-styles '(orderless-literal
                                      orderless-regexp)))
                                      ;; orderless-flex)))
#+end_src

** Consult

#+begin_src emacs-lisp :tangle "init.el"
  (use-package consult
    :ensure t
    :defer t)
#+end_src

** Corfu

#+begin_src emacs-lisp :tangle "init.el"

  (use-package corfu
    :ensure t

    :custom
    (corfu-cycle t) ;; Enable cycling through candidates
    (corfu-auto t)  ;; Automatically pop-up
    (corfu-auto-prefix 3)  ;; Require 3 characters for auto pop-up
    (corfu-auto-delay 0.5) ;; Wait half a second before pop-up
    (corfu-quit-at-boundary 'separator) ;; Keep pop-up open with separator
    (corfu-preview-current 'insert) ;; Do not preview candidates in buffer

    :init
    (global-corfu-mode) ;; Enable corfu everywhere
    (corfu-history-mode) ;; Save completion history
    )

#+end_src

** which-key

#+begin_src emacs-lisp :tangle "init.el"
  (use-package which-key
    :ensure t
    :config
    (which-key-setup-minibuffer)
    (which-key-mode))
#+end_src

** yasnippet

#+begin_src emacs-lisp :tangle "init.el"
  ;; Install yasnippet
  (use-package yasnippet
    :ensure t
    :defer t
    :config
    (yas-reload-all)
    :hook
    (prog-mode . yas-minor-mode))

  ;; Install official yasnippet snippets
  (use-package yasnippet-snippets
    :ensure t)
#+end_src

** GPTel

#+begin_src emacs-lisp :tangle "init.el"
  (use-package gptel
    :ensure t
    :defer t

    :config
    ;; Set Copilot as default backend
    (setq gptel-model 'claude-3.7-sonnet
          gptel-backend (gptel-make-gh-copilot "Copilot")))
#+end_src

** Elpaca wait

#+begin_src emacs-lisp :tangle "init.el"
  (elpaca-wait)
#+end_src

* Org-mode

Install latest org-mode.

#+begin_src emacs-lisp :tangle "init.el"
  (use-package org
    :ensure t
    :defer t)
#+end_src

** Prettify org-mode

Open org-files showing indented headings by default.

#+begin_src emacs-lisp :tangle "init.el"
  (setq org-startup-indented t)
#+end_src

Prettify markers and bullets:

#+begin_src emacs-lisp :tangle "init.el"
  ;; Use nice bullets for org-mode headers
  (use-package org-bullets
    :ensure t
    :after (org)
    :config
    (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1))))

  ;; Replace "-" in lists with a centered dot
  (font-lock-add-keywords 'org-mode
                          '(("^ *\\([-]\\) "
                             (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "â€¢"))))))

#+end_src

** Agenda files

Set in which files the org agenda should look for tasks.

#+begin_src emacs-lisp :tangle "init.el"
      (setq org-agenda-files (list "~/pCloudSync/org/agenda.org"
                                   "~/pCloudSync/org/todo.org"
                                   "~/pCloudSync/org/inbox.org"
                                   "~/pCloudSync/org/calendar.org"
                                   ))
#+end_src

** Org capture

#+begin_src emacs-lisp :tangle "init.el"
  (setq org-capture-templates
        '(("t" "Todo" entry (file+headline "~/pCloudSync/org/agenda.org" "Tasks")
           "* TODO %?\n  %i\n  %a\n")
          ("e" "Event" entry (file+headline "~/pCloudSync/org/agenda.org" "Events")
           "* %?\n  %^T\n")
          ("j" "Journal" entry (file+datetree "~/pCloudSync/org/journal.org")
           "* %?\nEntered on %U\n")
          ("i" "Idea" entry (file+headline "~/pCloudSync/org/inbox.org" "Ideas")
           "* %?\n %U\n")
          ))
#+end_src

* Notes
** Denote
#+begin_src emacs-lisp :tangle "init.el"
  (use-package denote
    :ensure t
    :init
    (setq denote-directory (expand-file-name "~/pCloudSync/org/notes")))
#+end_src
** Elpaca wait

#+begin_src emacs-lisp :tangle "init.el"
  (elpaca-wait)
#+end_src
* Programming
** Indentation

#+begin_src emacs-lisp :tangle "init.el"
  (setq-default indent-tabs-mode nil        ;; Stop using tabs to indent
                tab-always-indent 'complete ;; Indent first then try completions
                tab-width 2)                ;; Set 2 as tab width
  (setq indent-line-function 'insert-tab)
#+end_src

** Load languages in org-babel

#+begin_src emacs-lisp :tangle "init.el"
  (add-hook 'org-mode-hook (lambda ()
                             (org-babel-do-load-languages
                              'org-babel-load-languages
                              '(
                                (emacs-lisp . t)
                                (python . t)
                                (matlab . t)
                                ))))
#+end_src

** tree-sitter

#+begin_src emacs-lisp :tangle "init.el"
  (use-package tree-sitter
    :ensure t
    :after tree-sitter-langs
    :demand t
    :config
    (global-tree-sitter-mode)
    (add-hook 'tree-sitter-after-on-hook #'tree-sitter-hl-mode))

  (use-package tree-sitter-langs
    :ensure t
    :demand t)
#+end_src

*** Customize tree-sitter faces

The goal of this section is to reduce the number of colors used in code
highlighting.

#+begin_src emacs-lisp :tangle "init.el"
  (with-eval-after-load 'tree-sitter
    (let ((base00 (plist-get base16-stylix-theme-colors :base00))
          (base01 (plist-get base16-stylix-theme-colors :base01))
          (base02 (plist-get base16-stylix-theme-colors :base02))
          (base03 (plist-get base16-stylix-theme-colors :base03))
          (base04 (plist-get base16-stylix-theme-colors :base04))
          (base05 (plist-get base16-stylix-theme-colors :base05))
          (base06 (plist-get base16-stylix-theme-colors :base06))
          (base07 (plist-get base16-stylix-theme-colors :base07))
          (base08 (plist-get base16-stylix-theme-colors :base08))
          (base09 (plist-get base16-stylix-theme-colors :base09))
          (base0A (plist-get base16-stylix-theme-colors :base0A))
          (base0B (plist-get base16-stylix-theme-colors :base0B))
          (base0C (plist-get base16-stylix-theme-colors :base0C))
          (base0D (plist-get base16-stylix-theme-colors :base0D))
          (base0E (plist-get base16-stylix-theme-colors :base0E))
          (base0F (plist-get base16-stylix-theme-colors :base0F)))
      (custom-set-faces
       `(tree-sitter-hl-face:attribute             ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:comment               ((t (:foreground ,base02))))
       `(tree-sitter-hl-face:constant              ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:constant.builtin      ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:constructor           ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:doc                   ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:embedded              ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:escape                ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:function              ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:function.builtin      ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:function.call         ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:function.macro        ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:function.special      ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:keyword               ((t (:foreground ,base0E))))
       `(tree-sitter-hl-face:label                 ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:method                ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:method.call           ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:number                ((t (:foreground ,base0C))))
       `(tree-sitter-hl-face:operator              ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:property              ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:property.definition   ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:punctuation           ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:punctuation.bracket   ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:punctuation.delimiter ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:punctuation.special   ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:string                ((t (:foreground ,base0C))))
       `(tree-sitter-hl-face:string.special        ((t (:foreground ,base0C))))
       `(tree-sitter-hl-face:tag                   ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:type                  ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:type.argument         ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:type.builtin          ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:type.parameter        ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:type.super            ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:variable              ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:variable.builtin      ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:variable.parameter    ((t (:foreground ,base05))))
       `(tree-sitter-hl-face:variable.special      ((t (:foreground ,base05))))
       )))
#+end_src

** Formatter

#+begin_src emacs-lisp :tangle "init.el"
  (use-package format-all
    :ensure t
    :defer t)
#+end_src

** LSP

This block ensures that =eglot= is updated to the latest release.

#+begin_src emacs-lisp :tangle "init.el"
  ;; Ensure flymake is up to date
  (use-package flymake
    :ensure t
    :defer t)

  ;; Update eglot to latest version
  (use-package eglot
    :ensure t
    :defer t)
#+end_src

** Matlab

Install and configure Matlab mode:

#+begin_src emacs-lisp :tangle "init.el"
  (use-package matlab-mode
    :ensure t
    :defer t
    :init (setq matlab-indent-level 2  ;; Set indentation level to 2
                matlab-indent-function-body t) ;; Indent function bodies
    :config
    (evil-set-initial-state 'matlab-shell-mode 'emacs)
  )
#+end_src

** Latex

#+begin_src emacs-lisp :tangle "init.el"
  (use-package auctex
    :ensure t
    :defer t
    :hook
    (LaTeX-mode . outline-minor-mode) ;; enable code folding
    (LaTeX-mode . display-line-numbers-mode) ;; display line numbers
    (LaTeX-mode . yas-minor-mode) ;; enable yasnippet mode
    (LaTeX-mode . TeX-source-correlate-mode) ;; enable synctex
    (LaTeX-mode . flyspell-mode) ;; enable grammar checking
    (LaTeX-mode . flymake-mode) ;; enable syntax checking
    ;; Let eglot out of flymake in LaTeX-mode
    ((LaTeX-mode . (lambda ()
                   ;; Keep eglot out of flymake in LaTeX buffers
                   (setq-local eglot-stay-out-of '(flymake))))
                   ;; Enable eglot
                   (eglot-ensure))
    )
#+end_src

** Python

#+begin_src emacs-lisp :tangle "init.el"
  (use-package python
    :ensure nil ;; built-in
    :defer t

    :init
    (setq python-indent-guess-indent-offset t
          ;; Let python guess indentation silently
          python-indent-guess-indent-offset-verbose nil
          ;; Set default indentation level to 4
          python-indent-offset 4)

    :config
    (evil-set-initial-state 'inferior-python-mode 'emacs))
#+end_src

** Julia

#+begin_src emacs-lisp :tangle "init.el"
  (use-package julia-mode
    :ensure t
    :init
    (setq julia-indent-offset 2))
#+end_src
** Shell

#+begin_src emacs-lisp :tangle "init.el"
  ;; Set shell indentation to 2
  (setq sh-basic-offset 2)
#+end_src

** Nix

#+begin_src emacs-lisp :tangle "init.el"
  (use-package nix-mode
    :ensure t
    :defer t
    :mode "\\.nix\\'")
#+end_src

** Elpaca wait

#+begin_src emacs-lisp :tangle "init.el"
  (elpaca-wait)
#+end_src

* Custom functions

This section defines custom functions.

** my/keyboard-quit-dwim

This is from [[https://protesilaos.com/codelog/2024-11-28-basic-emacs-configuration/#h:e5e7b781-4b04-4c53-bf23-5e81789549ce][Protesilaos]]. 

#+begin_src emacs-lisp :tangle "init.el"
  (defun my/keyboard-quit-dwim ()
    "Do-What-I-Mean behaviour for a general `keyboard-quit'.

  The generic `keyboard-quit' does not do the expected thing when
  the minibuffer is open.  Whereas we want it to close the
  minibuffer, even without explicitly focusing it.

  The DWIM behaviour of this command is as follows:

  - When the region is active, disable it.
  - When a minibuffer is open, but not focused, close the minibuffer.
  - When the Completions buffer is selected, close it.
  - In every other case use the regular `keyboard-quit'."
    (interactive)
    (cond
     ((region-active-p)
      (keyboard-quit))
     ((derived-mode-p 'completion-list-mode)
      (delete-completion-window))
     ((> (minibuffer-depth) 0)
      (abort-recursive-edit))
     (t
      (keyboard-quit))))
#+end_src

** my/org-dwim-at-point

Execute an action in =org-mode= based on where the point is.

#+begin_src emacs-lisp :tangle "init.el"

  (defun my/org-dwim-at-point ()
    "Do-what-I-mean at point.
  Handles common Org elements intuitively."
    (interactive)
    (let* ((context (org-element-context))
           (type (org-element-type context)))
      (cond
       ;; Check for checkbox inside a list item
       ((and (eq type 'item)
             (org-element-property :checkbox context))
        (org-toggle-checkbox))

       ((eq type 'headline)
        (org-todo))

       ((eq type 'link)
        (org-open-at-point))

       ((eq type 'footnote-reference)
        (org-footnote-goto-definition
         (org-element-property :label context)))

       ((eq type 'footnote-definition)
        (org-footnote-goto-previous-reference
         (org-element-property :label context)))

       ((eq type 'table-row)
        (org-table-recalculate))

       ((memq type '(src-block inline-src-block))
        (org-babel-execute-src-block))

       ((memq type '(latex-fragment latex-environment))
        (org-latex-preview))

       (t
        (message "No specific action for this element")))))
#+end_src
* Keybindings

** Generic

#+begin_src emacs-lisp :tangle "init.el"

  ;; Keyboard quit
  (define-key global-map (kbd "C-g") #'my/keyboard-quit-dwim)

  ;; Org dwim at point
  (with-eval-after-load 'evil
    (add-hook 'org-mode-hook
              (lambda ()
                (evil-define-key 'normal org-mode-map
                  (kbd "RET") #'my/org-dwim-at-point))))

  ;; Window movements
  (global-unset-key (kbd "M-<left>"))
  (global-unset-key (kbd "M-<down>"))
  (global-unset-key (kbd "M-<up>"))
  (global-unset-key (kbd "M-<right>"))
  (define-key evil-normal-state-map (kbd "M-<left>")  'evil-window-left)
  (define-key evil-normal-state-map (kbd "M-<down>")  'evil-window-down)
  (define-key evil-normal-state-map (kbd "M-<up>")    'evil-window-up)
  (define-key evil-normal-state-map (kbd "M-<right>") 'evil-window-right)

  ;; org-agenda-earlier and later
  (with-eval-after-load 'org-agenda
    (define-key org-agenda-mode-map (kbd "[") #'org-agenda-earlier)
    (define-key org-agenda-mode-map (kbd "]") #'org-agenda-later))
#+end_src

** Evil: general.el

#+begin_src emacs-lisp :tangle "init.el"
  (use-package general
    :ensure t
    :demand t
    :after evil

    :config
    (general-define-key
     :states '(normal)
     :prefix "SPC"
     ;; file
     "f f" #'find-file
     "f r" #'recentf-open
     ;; consult
     "c d" #'consult-fd
     "c f" #'consult-flymake
     "c l" #'consult-line
     "c m" #'consult-man
     "c r" #'consult-ripgrep
     ;; buffer
     "b b" #'consult-buffer
     "b k" #'kill-this-buffer
     ;; windows
     "w <left>" #'evil-window-left
     "w <down>" #'evil-window-down
     "w <up>" #'evil-window-up
     "w <right>" #'evil-window-right
     "w h" #'evil-window-split
     "w v" #'evil-window-vsplit
     "w w" #'evil-window-next
     "W" #'evil-window-prev
     "w k" #'kill-buffer-and-window
     ;; frames
     "F F" #'make-frame
     "F f" #'find-file-other-frame
     "F k" #'delete-frame
     ;; magit
     "g g" #'magit-status
     ;; org-agenda
     "a a" #'org-agenda
     "a c" #'org-capture
     ;; denote
     "n n" #'denote
     "n g" #'denote-grep
     ))
#+end_src

** Commands
*** :k
#+begin_src emacs-lisp :tangle "init.el"
(defun my/kill-current-buffer ()
  "Kill the current buffer without confirmation."
  (interactive)
  (kill-buffer (current-buffer)))

(evil-ex-define-cmd "k" 'my/kill-current-buffer)
#+end_src
* Emacs server

#+begin_src emacs-lisp :tangle "init.el"
  ;; (use-package server
  ;;   :ensure nil
  ;;   :config
  ;;   (setq server-client-instructions nil)
  ;;   (unless (server-running-p)
  ;;     (server-start)))
#+end_src

